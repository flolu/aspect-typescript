load("@aspect_rules_swc//swc:defs.bzl", "swc")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@aspect_rules_js//js:defs.bzl", "js_binary")
load("@bazel_skylib//lib:partial.bzl", "partial")
load("@io_bazel_rules_docker//container:container.bzl", "container_image")
load("@io_bazel_rules_docker//container:layer.bzl", "container_layer")
load("@aspect_rules_jest//jest:defs.bzl", "jest_test")
load("//:js_image_layer.bzl", "js_image_layer")

ts_project(
    name = "app",
    srcs = ["main.ts"],
    composite = True,
    declaration = True,
    transpiler = partial.make(
        swc,
        swcrc = "//:swcrc",
    ),
    tsconfig = "//:tsconfig",
    deps = [
        "//:node_modules/@org/lib",
        "//:node_modules/@types/node",
    ],
)

ts_project(
    name = "app_test",
    srcs = ["main.test.ts"],
    composite = True,
    declaration = True,
    tsconfig = "//:tsconfig",
    deps = [
        ":app",
        "//:node_modules/@types/jest",
    ],
)

jest_test(
    name = "test",
    config = "//:jest.config.js",
    data = [":app_test"],
)

js_binary(
    name = "bin",
    data = [":app"],
    entry_point = "main.js",
)

js_image_layer(
    name = "layers",
    binary = ":bin",
    root = "/app",
    tags = [],
)

container_layer(
    name = "app_layer",
    tars = [":layers/app.tar"],
)

container_layer(
    name = "node_modules_layer",
    tars = [":layers/node_modules.tar"],
)

container_image(
    name = "image",
    architecture = "amd64",
    base = "@debian_amd64//image",
    # /{root of js_image_layer}/{package_name()}/{name of js_binary}.sh
    cmd = ["/app/app/bin.sh"],
    entrypoint = ["bash"],
    layers = [
        ":app_layer",
        ":node_modules_layer",
    ],
)
